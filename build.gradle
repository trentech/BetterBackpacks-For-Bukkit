plugins {
	id "java"
	id "com.gradleup.shadow" version "9.3.0"
	id "eclipse"
}

group = pluginGroup
version = pluginVersion

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
    gradlePluginPortal()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "codemc-repo"
        url = "https://repo.codemc.org/repository/maven-public/"
    }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.21.11-R0.1-SNAPSHOT"
    implementation "de.tr7zw:item-nbt-api:2.15.5"
    implementation fileTree(dir: "libs", include: ["*.jar"])
}

tasks.named("shadowJar") {
    dependencies {
        include(dependency("de.tr7zw:item-nbt-api:2.15.5"))
    }
    relocate "de.tr7zw.changeme.nbtapi", "org.trentech.de.tr7zw.nbtapi"
    archiveFileName.set("betterbackpacks-${project.version}.jar")
}

tasks.named("build") {
    dependsOn(shadowJar)
}